plugins {
    id 'application'
    id 'java'
    id 'jacoco'
}

group 'uk.ashleybye'

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.2'
}

wrapper {
    gradleVersion = '5.0'
}

application {
    mainClassName = 'uk.ashleybye.httpserver.HttpServer'
}

jar {
    manifest {
        attributes(
                'Class-Path': configurations.compile.collect { it.getName() }.join(' '),
                'Main-Class': 'uk.ashleybye.httpserver.HttpServer'
        )
    }
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
    destinationDir = file("$buildDir")
}

test {
    useJUnitPlatform()
}

task acceptanceTests {
    doLast {
        exec {
            workingDir 'acceptance-tests'
            commandLine 'bundle', 'exec', 'spinach'
        }
    }
}

jacoco {
    toolVersion = "0.8.2"
}

jacocoTestReport {
    group = "Reporting"
    reports {
        xml.enabled true
        html.enabled true
    }
}

jacocoTestCoverageVerification {
    violationRules.rule {
        limit { counter = 'INSTRUCTION'; minimum = 0.75 }
    }
}

check.dependsOn jacocoTestReport
check.dependsOn jacocoTestCoverageVerification
